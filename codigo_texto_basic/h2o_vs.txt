Define LibPub h2o_vs(p,t,tipo)=
Func
© tabela A-4 (vapor sobreaquecido), TabH2OA4: linhas(1,24)
© modo de uso: H2O_vs(bar,@dC,{"v","u","h","s"})
© exemplo: H2O_vs(0.5,32,{"v","s"})

Local  i,a,b,prop,relcol,col1,col2,propa,propb,lin1,lin2,dtipo

dim(tipo)=:dtipo

For  i,1,dtipo
  If tipo[i]="v" Then
    2=:relcol[i]
  ElseIf tipo[i]="u" Then
    3=:relcol[i]
  ElseIf tipo[i]="h" Then
    4=:relcol[i]
  ElseIf tipo[i]="s" Then
    5=:relcol[i]
  EndIf
EndFor

© Procurar na gama de pressoes
0=:a
0=:b
For  i,24,1,-1
  If p>=asstermod.tabh2oa4[6,i] Then
    i=:a
    Exit
  EndIf
EndFor

If a/=0 Then
 For  i,a,24
  If p<=asstermod.tabh2oa4[6,i] Then
    i=:b
    Exit
  EndIf
 EndFor
EndIf

© Verificar validade dos resultados obtidos
If a=0 or b=0 Then
  undef+0*relcol=:prop
Else
  asstermod.tabh2oa4[3,a]=:col1
  col1+relcol-1=:col2
  asstermod.tabh2oa4[2,a]=:lin1
  asstermod.tabh2oa4[4,a]=:lin2
  If a=b Then
    asstermod.h2o_get(lin1,lin2,col1,col2,t,"asstermod.TabH2OA4")=:prop
  Else
    asstermod.h2o_get(lin1,lin2,col1,col2,t,"asstermod.TabH2OA4")=:propa

    asstermod.tabh2oa4[3,b]=:col1
    col1+relcol-1=:col2
    asstermod.tabh2oa4[2,b]=:lin1
    asstermod.tabh2oa4[4,b]=:lin2
    asstermod.h2o_get(lin1,lin2,col1,col2,t,"asstermod.TabH2OA4")=:propb

    asstermod.tabh2oa4[6,a]=:lin1
    asstermod.tabh2oa4[6,b]=:lin2
    asstermod.interpol(lin1,propa,lin2,propb,p)=:prop
  EndIf
EndIf

Return  prop
EndFunc
